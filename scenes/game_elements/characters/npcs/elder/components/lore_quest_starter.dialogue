# SPDX-FileCopyrightText: The Threadbare Authors
# SPDX-License-Identifier: MPL-2.0
~ start
LoreQuest Elder: [[Hi|Hello|Greetings]], StoryWeaver. We've been waiting for you. Our world is unraveling!
LoreQuest Elder: We need you to recover the Sacred Elements: the threads of Memory, Imagination, and Spirit! Will you help us?
do show_storybook()
if chosen_quest == null:
	% LoreQuest Elder: Please stay. Our world will perish without your help!
	% LoreQuest Elder: I hope to see you again. We really need you!
	% LoreQuest Elder: The people of Threadbare are counting on you. I hope you change your mind!
elif chosen_quest.resource_path in GameState.completed_quests:
	# Custom dialogues for completed quests
	if chosen_quest.resource_path == "res://scenes/quests/lore_quests/quest_001/quest.tres":
		LoreQuest Elder: Ah, the Song Sanctuary sings once more... You’ve restored its melody to the world.
		LoreQuest Elder: The stones hum with life again. Music returns to Threadbare thanks to you, StoryWeaver.

	elif chosen_quest.resource_path == "res://scenes/quests/lore_quests/quest_002/quest.tres":
		LoreQuest Elder: The Void trembles now, pushed back by your courage.

	elif chosen_quest.resource_path == "res://scenes/quests/lore_quests/quest_003/quest.tres":
		LoreQuest Elder: You’ve gathered both Imagination and Spirit... the twin flames of creation.
		LoreQuest Elder: The Loom will remember your deeds, StoryWeaver.

	else:
		% LoreQuest Elder: Well done, traveler. Another story is restored.
		% LoreQuest Elder: Hope grows stronger with every thread you weave.
else:
	LoreQuest Elder: We believe in you, StoryWeaver.
=> END
~ go_to_loom
LoreQuest Elder: Go on, StoryWeaver: take the threads to the Eternal Loom.
=> END
~ threads_incorporated
LoreQuest Elder: Thank you, StoryWeaver! The fabric of the world slowly heals!
LoreQuest Elder: Go forth: our world still needs your help.
=> END
