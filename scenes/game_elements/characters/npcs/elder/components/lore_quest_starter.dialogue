# SPDX-FileCopyrightText: The Threadbare Authors
# SPDX-License-Identifier: MPL-2.0
~ start
LoreQuest Elder: [[Hi|Hello|Greetings]], StoryWeaver. We've been waiting for you. Our world is unraveling!
LoreQuest Elder: We need you to recover the Sacred Elements: the threads of Memory, Imagination, and Spirit! Will you help us?
do show_storybook()
if chosen_quest == null:
	% LoreQuest Elder: Please stay. Our world will perish without your help!
	% LoreQuest Elder: I hope to see you again. We really need you!
	% LoreQuest Elder: The people of Threadbare are counting on you. I hope you change your mind!
elif chosen_quest.resource_path in GameState.completed_quests:
	# Custom dialogues for completed quests
	if chosen_quest.resource_path == "res://scenes/quests/lore_quests/quest_001/quest.tres":
		% LoreQuest Elder: Ah, the Song Sanctuary sings once more... a melody you already helped restore.
		% LoreQuest Elder: If you wish to revisit its echoes, traveler, the sanctuary always welcomes a gentle hand to tune its harmony again.

	elif chosen_quest.resource_path == "res://scenes/quests/lore_quests/quest_002/quest.tres":
		% LoreQuest Elder: The Void still stirs at the edges… even after the balance you once brought.
		% LoreQuest Elder: Should you feel ready to face it again, your presence can steady the archipelago’s stories once more.

	elif chosen_quest.resource_path == "res://scenes/quests/lore_quests/quest_003/quest.tres":
		% LoreQuest Elder: You’ve walked this path before—where Imagination and Spirit intertwine.
		% LoreQuest Elder: If you seek its lessons again, the Loom is always willing to be rewoven by steady hands.

	else:
		% LoreQuest Elder: This tale is one you already helped mend, traveler… yet its threads shift with each return.
		% LoreQuest Elder: If your heart pulls you back to it, the story will open itself to you once again.
else:
	LoreQuest Elder: We believe in you, StoryWeaver.
=> END
~ go_to_loom
LoreQuest Elder: Go on, StoryWeaver: take the threads to the Eternal Loom.
=> END
~ threads_incorporated
LoreQuest Elder: Thank you, StoryWeaver! The fabric of the world slowly heals!
LoreQuest Elder: Go forth: our world still needs your help.
=> END
