# SPDX-FileCopyrightText: The Threadbare Authors
# SPDX-License-Identifier: MPL-2.0
~ start
StoryQuest Elder: [[Hi|Hello|Greetings]], StoryWeaver. We've been waiting for you. Our world is unraveling!
StoryQuest Elder: We need you to recover the Sacred Elements: the threads of Memory, Imagination, and Spirit! Will you help us?
do show_storybook()
if chosen_quest == null:
	% StoryQuest Elder: Please stay. Our world will perish without your help!
	% StoryQuest Elder: I hope to see you again. We really need you!
	% StoryQuest Elder: The people of Threadbare are counting on you. I hope you change your mind!
elif chosen_quest.resource_path in GameState.completed_quests:
	# Custom dialogues for completed quests
	if chosen_quest.resource_path == "res://scenes/quests/story_quests/el_juguete_perdido/quest.tres":
		% StoryQuest Elder: Has rescatado a Apolo antes… y gracias a ti volvió sano y salvo.
		% StoryQuest Elder: Pero ese chico siempre encuentra nuevas travesuras… quizá necesite tu guía otra vez.
		% StoryQuest Elder: Si deseas volver al bosque, podrás revivir la búsqueda y perfeccionar tu camino.

	elif chosen_quest.resource_path == "res://scenes/quests/story_quests/shjourney/quest.tres":
		% StoryQuest Elder: Ya has enfrentado al bosque que devora recuerdos… pocos regresan de él.
		% StoryQuest Elder: Si quieres regresar, las sendas del bosque estarán listas para probarte de nuevo.

	elif chosen_quest.resource_path == "res://scenes/quests/story_quests/stella/quest.tres":
		% StoryQuest Elder: Stella continues to look to the sky with hope thanks to you.
		% StoryQuest Elder: If you accompany her again, you will be able to experience the story from a different light.

	elif chosen_quest.resource_path == "res://scenes/quests/story_quests/verso/quest.tres":
		% StoryQuest Elder: Tu apoyo le devolvió la confianza a Nora cuando más lo necesitaba.
		% StoryQuest Elder: Pero el conocimiento siempre pone nuevas pruebas a quienes regresan.
		% StoryQuest Elder: Si decides intentarlo otra vez, podrás dominar cada paso del desafío.

	else:
		% StoryQuest Elder: You have walked this story before… yet tales shift with every return.
		% StoryQuest Elder: If you wish to relive it, the thread of this adventure will open itself to you once more.
else:
	StoryQuest Elder: We believe in you, StoryWeaver. Become the hero, and see where the path leads.
=> END
~ go_to_loom
StoryQuest Elder: Go on, StoryWeaver: take the threads to the Eternal Loom.
=> END
~ threads_incorporated
StoryQuest Elder: Thank you, StoryWeaver! The fabric of the world slowly heals!
StoryQuest Elder: Go forth: our world still needs your help.
=> END
