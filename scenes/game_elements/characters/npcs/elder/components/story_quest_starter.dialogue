# SPDX-FileCopyrightText: The Threadbare Authors
# SPDX-License-Identifier: MPL-2.0
~ start
StoryQuest Elder: [[Hi|Hello|Greetings]], StoryWeaver. We've been waiting for you. Our world is unraveling!
StoryQuest Elder: We need you to recover the Sacred Elements: the threads of Memory, Imagination, and Spirit! Will you help us?
do show_storybook()
if chosen_quest == null:
	% StoryQuest Elder: Please stay. Our world will perish without your help!
	% StoryQuest Elder: I hope to see you again. We really need you!
	% StoryQuest Elder: The people of Threadbare are counting on you. I hope you change your mind!
elif chosen_quest.resource_path in GameState.completed_quests:
	# Custom dialogues for completed quests
	if chosen_quest.resource_path == "res://scenes/quests/story_quests/el_juguete_perdido/quest.tres":
		% StoryQuest Elder: You have rescued Apollo before… and thanks to you he returned safe and sound.
		% StoryQuest Elder: But that boy always finds new mischief… he may need your guidance once more.
		% StoryQuest Elder: If you wish to return to the forest, you can relive the quest and refine your path.

	elif chosen_quest.resource_path == "res://scenes/quests/story_quests/shjourney/quest.tres":
		% StoryQuest Elder: You have already faced the memory-devouring forest… few ever return from it.
		% StoryQuest Elder: If you wish to go back, the forest trails will be ready to test you once again.

	elif chosen_quest.resource_path == "res://scenes/quests/story_quests/stella/quest.tres":
		% StoryQuest Elder: Stella keeps looking to the sky with hope thanks to you.
		% StoryQuest Elder: If you accompany her again, you’ll be able to walk through the story in a new light.

	elif chosen_quest.resource_path == "res://scenes/quests/story_quests/verso/quest.tres":
		% StoryQuest Elder: Your support restored Nora’s confidence when she needed it most.
		% StoryQuest Elder: But knowledge always brings new trials to those who return.
		% StoryQuest Elder: If you choose to try again, you’ll be able to master every step of the challenge.
	else:
		% StoryQuest Elder: You have walked this story before… yet tales shift with every return.
		% StoryQuest Elder: If you wish to relive it, the thread of this adventure will open itself to you once more.
else:
	StoryQuest Elder: We believe in you, StoryWeaver. Become the hero, and see where the path leads.
=> END
~ go_to_loom
StoryQuest Elder: Go on, StoryWeaver: take the threads to the Eternal Loom.
=> END
~ threads_incorporated
StoryQuest Elder: Thank you, StoryWeaver! The fabric of the world slowly heals!
StoryQuest Elder: Go forth: our world still needs your help.
=> END
